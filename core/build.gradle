apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

sourceSets.main.java.srcDirs = [ "src/" ]
sourceCompatibility = 1.6

ext {
    gdxVersion = '1.9.2'
    dragomeVersion = '0.96-beta3-SNAPSHOT'
    isSnapshot = '-SNAPSHOT'
}

group = projectGroupId
archivesBaseName = "gdx-backend-dragome"
version = "$gdxVersion$isSnapshot"

configurations {
    deployerJars
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

jar {
    from project.sourceSets.main.allSource
    from project.sourceSets.main.output
    baseName = 'gdx-backend-dragome'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

eclipse.project.name = "gdx-backend-dragome"

dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.dragome:dragome-js-jre:$dragomeVersion"
    compile "com.dragome:dragome-guia:$dragomeVersion"
    compile "com.dragome:dragome-web:$dragomeVersion"

    deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
    deployerJars "org.apache.maven.wagon:wagon-http:2.2"
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'LibGDX Dragome Backend'
        packaging 'jar'
        description 'Dragome backend for LibGDX applications.'
        url 'http://github.com/czyzby/gdx-dragome'

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        
        scm {
          connection 'scm:git:git@github.com:czyzby/gdx-dragome.git'
          developerConnection 'scm:git:git@github.com:czyzby/gdx-dragome.git'
          url 'http://github.com/czyzby/gdx-dragome/'
        }

        developers {
          developer {
            id 'mj'
            name 'MJ'
            email 'john.hervicc@gmail.com'
          }
          developer {
            id 'fp'
            name 'Fernando Petrola'
            email 'fernando.petrola@dragome.com'
          }
        }
      }
    }
  }
}
